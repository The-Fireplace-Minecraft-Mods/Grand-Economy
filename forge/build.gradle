buildscript {
    repositories {
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'

//Forge-specific build code goes in this file: Dependencies, mappings/forge versions, manifest data, etc.

allprojects {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            srcDir '../common/src/main/java'
        }
        resources {
            srcDir 'src/main/resources'
            srcDir '../common/src/main/resources'
        }
    }
}

archivesBaseName = project.hasProperty("outputJarBaseName") ? project.outputJarBaseName : "forge"

minecraft {
    mappings channel: 'stable', version: '60-1.15'
    
    runs {
        client = {
            properties 'forge.logging.markers': 'SCAN,REGISTRIES,REGISTRYDUMP'
            properties 'forge.logging.console.level': 'debug'
            workingDirectory project.file('run').canonicalPath
            source sourceSets.main
        }
        server = {
            properties 'forge.logging.markers': 'SCAN,REGISTRIES,REGISTRYDUMP'
            properties 'forge.logging.console.level': 'debug'
            workingDirectory project.file('run').canonicalPath
            source sourceSets.main
        }
    }
}

dependencies {
	minecraft 'net.minecraftforge:forge:1.15.2-31.0.14'
    compile 'org.spongepowered:spongeapi:7.1.0'
    /*compile (group: "com.forgeessentials", name: "forgeessentials", version: "1.4.6-SNAPSHOT", classifier: "dev", changing: true) {
        exclude module: "unspecified"
    }*/
    compile "org.spigotmc:spigot-api:1.15.2-R0.1-SNAPSHOT"
    compile group: "net.milkbowl.vault", name: "VaultAPI", version: "1.7"
    //compile name:'enderpay-1.12.2-1.0.1.0-deobf'
}

repositories {
    jcenter()
    mavenCentral()
    // Sponge
    maven { url 'https://repo.spongepowered.org/maven' }
    //Forge Essentials
    maven {
        name = "forge"
        url = "http://files.minecraftforge.net/maven"
    }
    //Forge Essentials 2
    maven {
        name = "sk89q"
        url = "http://maven.sk89q.com/repo/"
    }
    //Spigot
    maven { url = ("https://hub.spigotmc.org/nexus/content/groups/public/") }
    // Vault
    maven { url 'https://jitpack.io' }
    flatDir {
        dirs 'libs'
    }
}

jar {
    manifest {
        attributes(["Specification-Title": "grandeconomy",
                    "Specification-Vendor": "The_Fireplace",
                    "Specification-Version": "24.0", // We are version 1 of the modlauncher specification
                    "Implementation-Title": "Grand Economy",
                    "Implementation-Version": "${version}",
                    "Implementation-Vendor" : "The_Fireplace",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")],)
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

artifacts {
    archives jar
    archives deobfJar
}
