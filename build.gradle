buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "Modmuss50"
            url = "http://maven.modmuss50.me/"
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'ModsIOUpload:ModsIOUpload:+'
        //Shadow 1.2.4 is the last version of Shadow supporting Gradle 2.X
        classpath "com.github.jengelman.gradle.plugins:shadow:1.2.4"
    }
}

plugins {
    id "com.matthewprenger.cursegradle" version "1.4.0"
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'net.cazzar.gradle.modsio'
apply plugin: 'com.github.johnrengelman.shadow'

version = "1.3.5"
group = "the_fireplace.grandeconomy"
archivesBaseName = "GrandEconomy-1.12.2"

allprojects {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

minecraft {
    version = "1.12.2-14.23.5.2838"
    runDir = "run"
    replace '${version}', project.version
    mappings = "stable_39"
	
	clientRunArgs += "--uuid=0b1ec5ad-cb2a-43b7-995d-889320eb2e5b --username=The_Fireplace"
}

dependencies {
    provided 'org.spongepowered:spongeapi:7.1.0'
    provided group: "com.forgeessentials", name: "forgeessentials", version: "1.4.4-SNAPSHOT", classifier: "dev", changing: true
    provided "org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT"
    compile("net.milkbowl.vault:VaultAPI:1.7") {
        exclude module: "*"
    }
}

repositories {
    jcenter()
    mavenCentral()
    // Sponge
    maven { url 'https://repo.spongepowered.org/maven' }
    //Forge Essentials
    maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    maven {
		name = "sk89q"
		url = "http://maven.sk89q.com/repo/"
	}
	//Spigot
	maven { url = ("https://hub.spigotmc.org/nexus/content/groups/public/") }
	// Vault
	maven { url 'http://nexus.hc.to/content/repositories/pub_releases' }
}

shadowJar {
    classifier = ''
    exclude 'enderpay*.jar'
}

processResources
{
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

artifacts {
    archives jar
    archives deobfJar
    archives sourceJar
}

curseforge {
  apiKey = project.hasProperty("curseForgeApiKey")? project.curseForgeApiKey : "empty"
  project {
    id = '312141'
    changelog = file('changelog.txt')
    releaseType = 'release'
    addGameVersion '1.12.2'
    addGameVersion '1.12.1'
    addGameVersion '1.12'
	addGameVersion 'Java 8'
    mainArtifact(shadowJar) {
        displayName = shadowJar.archiveName.replace(".jar", "")
    }
    addArtifact(sourceJar) {
        displayName = sourceJar.archiveName.replace(".jar", "")
        changelog = "*This is a file for mod developers. If you don't know what to do with it, don't use it!*"
    }
    addArtifact(deobfJar) {
        displayName = deobfJar.archiveName.replace(".jar", "")
        changelog = "*This is a file for mod developers. If you don't know what to do with it, don't use it!*"
    }
  }
}

modsIO {
    key = project.hasProperty("modsioApiKey")? project.modsioApiKey : "empty"
    project{
		artifact = new File(shadowJar.destinationDir, archivesBaseName+"-"+version+".jar")
		modid = "1278"
		minecraft = "1.12.2"
		try{changelog = file('changelog.txt').text}catch(Exception e){changelog=""}
		tag = ""
		current = true
    }
}
